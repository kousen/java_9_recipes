plugins {
    id 'java-library'
    id 'idea'
    id 'eclipse'
}

ext.libs = [
        gson: 'com.google.code.gson:gson:2.13.0',
        hamcrest: 'org.hamcrest:hamcrest:3.0',
        junit_api: 'org.junit.jupiter:junit-jupiter:5.13.4',
]

allprojects {
    group 'com.kousenit'
    version '1.0'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    repositories {
        mavenCentral()
    }
}

dependencies {
    // JUnit BOM for consistent versions
    testImplementation platform("org.junit:junit-bom:5.13.4")
    
    implementation libs.gson
    testImplementation libs.junit_api
    testImplementation libs.hamcrest
    
    // Required for Gradle test execution
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}

compileJava {
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}
